plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    signingConfigs {
        relase {
        }
    }
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
            applicationId "com.paulovap.pinelangandroid"
            minSdkVersion 21
            targetSdkVersion 29
            versionCode 1
            versionName '1.0'
            vectorDrawables.useSupportLibrary = true
            testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        signingConfig signingConfigs.debug
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        apiVersion = '1.3' // Compose compiler based on 1.4 but set 1.3 for compatibility
        allWarningsAsErrors = true
    }
    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerVersion '1.3.70-dev-withExperimentalGoogleExtensions-20200424'
        kotlinCompilerExtensionVersion compose_version
    }

    packagingOptions {
        exclude 'META-INF/io.netty.versions.properties'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-notice.md'
        exclude 'META-INF/LICENSE.md'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/INDEX.LIST'
        exclude("META-INF/*.kotlin_module")
    }
}

configurations {
    ktlint
}

dependencies {

    implementation project(path: ":core")
    implementation project(path: ":lsp")

    implementation("io.coil-kt:coil:0.11.0")
    implementation "dev.chrisbanes.accompanist:accompanist-coil:0.1.6"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.google.android.material:material:1.1.0'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation "androidx.compose:compose-runtime:$compose_version"
    implementation "androidx.ui:ui-core:$compose_version"
    implementation "androidx.ui:ui-layout:$compose_version"
    implementation "androidx.ui:ui-material:$compose_version"
    implementation "androidx.ui:ui-material-icons-extended:$compose_version"
    implementation "androidx.ui:ui-foundation:$compose_version"
    implementation "androidx.ui:ui-animation:$compose_version"
    implementation "androidx.ui:ui-tooling:$compose_version"
    implementation 'androidx.appcompat:appcompat:1.3.0-alpha01'
    implementation 'androidx.activity:activity-ktx:1.1.0'
    implementation 'androidx.core:core-ktx:1.5.0-alpha01'

    implementation "androidx.ui:ui-livedata:$compose_version"

    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:2.3.0-alpha05"

    androidTestImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation "androidx.ui:ui-core:$compose_version"
    androidTestImplementation "androidx.ui:ui-test:$compose_version"

    ktlint 'com.pinterest:ktlint:0.37.0'

    // Flipper
    debugImplementation 'com.facebook.flipper:flipper:0.48.0'
    debugImplementation 'com.facebook.soloader:soloader:0.9.0'
    releaseImplementation 'com.facebook.flipper:flipper-noop:0.48.0'
}

task ktlint(type: JavaExec, group: 'verification') {
    description = 'Check Kotlin code style.'
    main = 'com.pinterest.ktlint.Main'
    classpath = configurations.ktlint
    args 'src/**/*.kt'
}

check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: 'formatting') {
    description = 'Fix Kotlin code style deviations.'
    main = 'com.pinterest.ktlint.Main'
    classpath = configurations.ktlint
    args '-F', 'src/**/*.kt'
}